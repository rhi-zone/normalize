; Ada tags query

(package_declaration
  name: (identifier) @name) @definition.module

(package_body
  name: (identifier) @name) @definition.module

(generic_package_declaration
  (package_declaration
    name: (identifier) @name)) @definition.module

(subprogram_declaration
  (function_specification
    name: (identifier) @name)) @definition.function

(subprogram_declaration
  (procedure_specification
    name: (identifier) @name)) @definition.function

(subprogram_body
  (function_specification
    name: (identifier) @name)) @definition.function

(subprogram_body
  (procedure_specification
    name: (identifier) @name)) @definition.function

(expression_function_declaration
  (function_specification name: (identifier) @name)) @definition.function

(full_type_declaration
  (identifier) @name) @definition.type

(private_type_declaration
  (identifier) @name) @definition.type

(incomplete_type_declaration
  (identifier) @name) @definition.type
